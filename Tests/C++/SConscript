# -*- python -*-
import os
import sys

from SCons.Script import Environment

env = Environment(tools = ['default', 'bspline', 'testing'])

sources = env.Split('''
bspline.cpp
options.cpp
''')

bspline = env.Program('bspline', sources)

env.Default(bspline)

env['BSPLINE'] = bspline
datadir = env.Dir('../Data')
outputdir = env.Dir('./output')
baselinedir = env.Dir('./baseline')

# Create builders for all of the test outputs and a diff between the output
# and the baseline.
for cutoff in [5, 30, 0]:
    for bc in [0, 1, 2]:
        for field in ['temps', 'wdir', 'wspd']:
            ofile = f"{outputdir}/spline-{field}-{cutoff}-{bc}.out"
            bfile = f"{baselinedir}/spline-{field}-{cutoff}-{bc}.out"
            dfile = f"{datadir}/sample.{field}"
            ofile = env.Command(ofile, [bspline, dfile], "${SOURCES[0]}"
                                f" -w {cutoff} -b {bc} "
                                "-i ${SOURCES[1]} -o $TARGETS")
            env.DefaultTest(env.Diff([f"{bfile}", ofile]))
